# -*- mode: shell-script -*-

unsetopt correct
unsetopt correct_all
export DISABLE_CORRECTION="true"

# don't prompt when redirecting output with >
# http://unix.stackexchange.com/a/212135
setopt clobber

# require confirmation before closing session with C-d
# http://zsh.sourceforge.net/Intro/intro_16.html
setopt ignoreeof

# keep history to reasonable size for quick loading
# historical are saved in ~/.zlogs/
export HISTSIZE=3000

# http://stackoverflow.com/a/11200998/1163042
export WORDCHARS='*?_-.[]~&;!#$%^(){}<>'

autoload -U colors && colors

# https://zhimingwang.org/blog/2015-09-21-zsh-51-and-bracketed-paste.html
# fixes eg pasting in URLs to youtube-dl
if [[ $TERM == dumb ]]; then
    # turn off ZLE bracketed paste in dumb term
    unset zle_bracketed_paste
else
    # otherwise turn on ZLE bracketed-paste-magic
    autoload -Uz bracketed-paste-magic
    zle -N bracketed-paste bracketed-paste-magic
fi

# don't complain about vars defined by zsh plugins
# shellcheck disable=SC2154
export NO_COLOR="%{$reset_color%}"
# shellcheck disable=SC2154
export RED="%{$fg[red]%}"
export GREEN="%{$fg[green]%}"
export BLUE="%{$fg[blue]%}"
export CYAN="%{$fg[cyan]%}"

# custom syntax highlighting
zle_highlight=(paste:fg=blue)
# shellcheck disable=SC2154
ZSH_HIGHLIGHT_STYLES[alias]='fg=cyan'
ZSH_HIGHLIGHT_PATTERNS+=('rm -rf' 'fg=white,bold,bg=red')

# set up extra completions but don't enable by default as they're slow
# (most are set up automatically by zprezto)
# warn if custom directory is not set up
[ -d "$HOME/.zsh-completions" ] || echo "$0: $HOME/.zsh-completions not available"

# to enable, execute: $ compinit

fpath+="$HOME/.zsh-completions/conda-zsh-completion"
fpath+="$HOME/.zsh-completions/pip-completion/"

############ fzf ############

# https://github.com/junegunn/fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# TODO: check for a better solution
# override to include history timestamps
fzf-history-widget() {
    local selected num
    setopt localoptions noglobsubst
    # add fc -i option
    selected=( $(fc -i -l 1 | $(__fzfcmd) +s --tac +m -n2..,.. --tiebreak=index --toggle-sort=ctrl-r ${=FZF_CTRL_R_OPTS} -q "${LBUFFER//$/\\$}") )
    if [ -n "$selected" ]; then
        num=$selected[1]
        if [ -n "$num" ]; then
            zle vi-fetch-history -n $num
        fi
    fi
    zle redisplay
}

############ zsh prompt ############

# inspired by https://spin.atomicobject.com/2016/05/28/log-bash-history/
export PROMPT_COMMAND='if [ "$(id -u)" -ne 0 ]; then echo "$(date "+%Y-%m-%d %H:%M:%S") $(pwd) $(fc -ln -1)" >> ~/.zlogs/zhistory-$(date "+%Y-%m-%d").log; fi'

precmd() {
    eval "$PROMPT_COMMAND"
    # work around trailing newlines being swallowed by command substitution
    # http://stackoverflow.com/a/15184414/1163042
    # add newline only if non-null
    gp=$(git_prompt)
    [[ -n $gp ]] && gp=$gp$'\n'
    PROMPT="${NEWLINE}${gp}$GREEN%~ $NO_COLOR%(!.#.‚ùØ) "
}
