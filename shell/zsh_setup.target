# -*- mode: shell-script -*-

############ zsh options ############

unsetopt correct
unsetopt correct_all
export DISABLE_CORRECTION="true"

# https://unix.stackexchange.com/a/33995
# in interactive shell, make # function like a comment
setopt interactivecomments

# don't prompt when redirecting output with >
# http://unix.stackexchange.com/a/212135
setopt clobber

# http://zsh.sourceforge.net/Doc/Release/Options.html
setopt HIST_IGNORE_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE

# https://zhimingwang.org/blog/2015-09-21-zsh-51-and-bracketed-paste.html
# fixes eg pasting in URLs to curl
if [[ $TERM == dumb ]]; then
    # turn off ZLE bracketed paste in dumb term
    unset zle_bracketed_paste
else
    # otherwise turn on ZLE bracketed-paste-magic
    autoload -Uz bracketed-paste-magic
    zle -N bracketed-paste bracketed-paste-magic
fi

############ zsh env vars ############

# https://unix.stackexchange.com/questions/111718/command-history-in-zsh
export HISTFILE="$HOME/.zhistory"

# keep history to reasonable size for quick loading
export HISTSIZE=7000
export SAVEHIST=$HISTSIZE

# http://stackoverflow.com/a/11200998/1163042
export WORDCHARS='*?_-.[]~&;!#$%^(){}<>'

############ zsh colors and highlights ############

# https://stackoverflow.com/a/6159885/1163042
autoload -U colors && colors

# custom syntax highlighting
zle_highlight=(paste:fg=blue)

# don't complain about vars defined by zsh plugins
# shellcheck disable=SC2154
export NO_COLOR="%{$reset_color%}"
# shellcheck disable=SC2154
export RED="%{$fg[red]%}"
export GREEN="%{$fg[green]%}"
export BLUE="%{$fg[blue]%}"
export CYAN="%{$fg[cyan]%}"

# https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters/pattern.md
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main pattern)
ZSH_HIGHLIGHT_PATTERNS+=('rm -rf' 'fg=white,bold,bg=red')
typeset -gA ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[alias]='fg=cyan'

############ fzf ############

if test -f /usr/local/opt/fzf/shell/key-bindings.zsh; then
    alias fzh='rg --no-filename . ~/.zlogs | fzf --tac --no-sort'
    export FZF_DEFAULT_OPTS='--border --exact --height 60% --algo=v1 --no-mouse'
    export FZF_DEFAULT_COMMAND='fd --type f'

    # load fzf bindings from homebrew installation
    source "/usr/local/opt/fzf/shell/key-bindings.zsh"

    # https://github.com/junegunn/fzf/pull/1940
    export FZF_HIST_FIND_NO_DUPS=on
else
    echo "$0: fzf not loaded"
fi

############ zsh prompt ############

# for each command invocation, save date, current dir, and command in ~/.zlogs/
# inspired by https://spin.atomicobject.com/2016/05/28/log-bash-history/
[ -d "$HOME/.zlogs" ] || echo "$0: ~/.zlogs/ not present!"
export PROMPT_COMMAND='if [ "$(id -u)" -ne 0 ]; then echo "$(date "+%Y-%m-%d %H:%M:%S") $(pwd) $(fc -ln -1)" >> ~/.zlogs/zhistory-$(date "+%Y-%m-%d").log; fi'

precmd() {
    eval "$PROMPT_COMMAND"
    # work around trailing newlines being swallowed by command substitution
    # http://stackoverflow.com/a/15184414/1163042
    # add newline only if non-null
    gp=$(git_prompt)
    vp=$(virtualenv_prompt)
    extra="$gp$vp"
    [[ -n $extra ]] && extra=$extra$'\n'
    PROMPT="${NEWLINE}${extra}$GREEN%~ $NO_COLOR%(!.#.‚ùØ) "
}
